;----------------------------------------------------------------------------------------------
;
;             dP            d8888b.                   
;             88                `88                   
;  .d8888b. d8888P .d8888b. .aaadP' dP    dP .d8888b. 
;  Y8ooooo.   88   88'  `88 88'     88    88 Y8ooooo. 
;        88   88   88.  .88 88.     88.  .88       88 
;  `88888P'   dP   `88888P8 Y88888P `88888P' `88888P' 
;                                                                                                  
; © 2020 K N Crouse                                        
;----------------------------------------------------------------------------------------------
; 
; This extension contains all functions that allow anima1s to make decisions from their 
; genotypes. 
;
; List of all genotype cod0ns:
; 
; MAINTENANCE  CODE  DESCRIPTION                
;
; 2019-09-XX    a    is an adult
; 2019-09-XX    b    is bigger than myself
; 2019-09-XX    c    is cycling
; 2019-09-XX    d    is a plant (not an anima1)
; 2019-09-XX    e    is myself
; 2019-09-XX    f    is female
; 2019-09-XX    g    is a gestatee
; 2019-09-XX    h    is healthier than myself
; 2019-09-XX    i    is an infant
; 2019-09-XX    j    is a juvenile
; 2019-09-XX    k    is kin (relatedness > 0.2) ; target is 0.25 or more related
; 2019-09-XX    l    is lactating
; 2019-09-XX    m    is a male
; 2019-09-XX    n    is a group member
; 2019-09-XX    o    is another (not myself)
; 2019-09-XX    p    is pregnant
; 2019-09-XX    q    is not kin (relatedness <= 0.2)
; 2019-09-XX    r    is smaller than myself
; 2019-09-XX    s    is resting
; 2019-09-XX    t    is neither carrying or carried by myself
; 2019-09-XX    u    is a stranger (none-group member)
; 2019-09-XX    v    is alive
; 2019-09-XX    w    is dead
; 2019-09-XX    x    is sicker than myself
; 2019-09-XX    y    is carried by myself
; 2019-09-XX    z    is an anima1 (not a plant)
; 2019-09-XX    +    is carrying myself
; 2019-09-XX    α    has yellow signal on
; 2019-09-XX    å    has yellow signal off
; 2019-09-XX    β    has red signal on
; 2019-09-XX    ß    has red signal off
; 2019-09-XX    ɣ    has blue signal on
; 2019-09-XX    œ    has blue signal off
; 2019-09-XX    *    is observed during the day
; 2019-09-XX    -    is observed during the night
; 2019-09-XX    #    has color closest to #
; 2020-09-XX    .    is not resting
;         
; 2019-08-26   --> living.chance
; 2019-08-26   --> body-size
; 2019-08-26   --> body-shade
; XXXX-XX-XX   --> visual-angle
; XXXX-XX-XX   --> visual-range
; 2019-08-26   --> day-perception
; 2019-08-26   --> night-perception
; 2019-08-26   --> conception-chance
; 2020-04-XX   --> bite-capacity
; 2019-08-26   --> mutation-chance
; 2019-08-26   --> sex-ratio
; 2019-08-26   --> litter-size
; 2019-08-26   --> turn-right
; 2019-08-26   --> turn-left
; 2019-08-26   --> go-forward
; 2020-XX-XX   --> set-heading 
; 2020-XX-XX   --> set-heading-random
; 2020-XX-XX   --> hide
; XXXX-XX-XX   --> rest
; 2019-08-26   --> yellow-signal
; 2019-08-26   --> red-signal
; 2019-08-26   --> blue-signal
; 2019-08-26   --> check-infancy
; 2019-08-26   --> check-birth
; 2019-08-26   --> check-juvenility
; 2019-08-26   --> check-weaning
; 2019-08-26   --> check-adulthood   
;
; 2020-04-01   --> move-toward 
; 2020-04-01   --> move-away-from
; 2019-10-16   --> supply-to 
; 2019-10-16   --> demand-from  
; 2019-10-16   --> eat   
; 2019-09-XX   --> join 
; 2019-08-26   --> leave
; 2020-XX-XX   --> recruit
; 2020-XX-XX   --> kick-out
; 2019-09-XX   --> pick-up  
; 2019-10-16   --> put-down 
; 2020-04-XX   --> cling-to  
; 2020-04-XX   --> squirm-from 
; 2019-09-XX   --> help  
; 2019-09-XX   --> hurt 
; 2019-10-16   --> mate-with 
; 
;----------------------------------------------------------------------------------------------

;----------------------------------------------------------------------------------------------
; MAKE DECISIONS
;
; Reporter that returns a list of decision vectors generated from genotype.
;
; Caller: anima1
; ENVIRONMENT: list of turtles (both plants and anima1s) observed by the caller.
;----------------------------------------------------------------------------------------------

to-report sta7us-get-decisions [ my-environment ]
  
  let meself self
  let current-energy energy.supply
  let ego-status status-of meself
  let decision-vectors []
  let conspecifics-list []
  let status-list []

  let status-table table:make
  foreach my-environment [ obj ->
    if ( is-patch? obj ) [ table:put status-table ( [pmeta.id] of obj ) ( status-of obj ) ]
    if ( is-anima1? obj ) [ table:put status-table ( [meta.id] of obj ) ( status-of obj ) ]
  ]
  
  foreach (sentence chromosome.I chromosome.II) [ g ->
    
    if ( length g = 5 and is-string? item 1 g and is-string? item 2 g and is-string? item 3 g and is-number? item 4 g ) [ ; checks that allele correctly organized
      let gene-ego item 1 g
      let gene-other item 2 g
      let gene-action item 3 g
      let gene-weight item 4 g
      
      ; Check that first CODON matches self
      if first-string-is-part-of-last? ( gene-ego ) ( ego-status ) [
        
        ; Consider status-of others in the environment
        foreach my-environment [ other-guy ->
          let other-status table:get status-table ifelse-value ( is-patch? other-guy ) [ [pmeta.id] of other-guy ] [ [meta.id] of other-guy ]
          
          ; Check that second CODON matches other
          if first-string-is-part-of-last? ( gene-other ) ( other-status ) [
            
            let distance-to-target distance other-guy
            set distance-to-target ifelse-value ( distance-to-target > 1 ) [ distance-to-target ] [ 1 ]
            set decision-vectors lput (list meself other-guy ( sta7us-get-action gene-action ) precision ( gene-weight / ( distance-to-target ^ 2 )) 5 false ) decision-vectors 
            
          ]
        ]
      ]
    ]
  ]
  
  report decision-vectors
end

;----------------------------------------------------------------------------------------------
; GET MUTATION
;
; Returns a randomly selected codon to occasionally introduce novel mutations into a population.
;
; CALLER: anima1
;----------------------------------------------------------------------------------------------

to-report sta7us-get-mutation [ original-codon type-of-mutation ] ; numbers, letters, both
  report ( ifelse-value 
    ( is-number? original-codon and ( type-of-mutation = "numbers" or type-of-mutation = "both" ) ) [ get-updated-value original-codon ( one-of [ 1 -1 ] ) ]
    ( member? original-codon action-code-list and ( type-of-mutation = "letters" or type-of-mutation = "both" ) ) [ one-of action-code-list ]
    [ ifelse-value ( type-of-mutation = "letters" or type-of-mutation = "both" ) [ generate-status ] [ original-codon ]] ) 
end

;--------------------------------------------------------------------------------------------------------------------
; REPORTERS
;--------------------------------------------------------------------------------------------------------------------

to-report sta7us-get-action [ action-code ]

  report
  (ifelse-value
    ( action-code = "MNT" or action-code = "maintain-body" or action-code = "living-chance" ) [ "living-chance" ]
    ( action-code = "GRW" or action-code = "body-size" ) [ "body-size" ]
    ( action-code = "BSD" or action-code = "body-shade" ) [ "body-shade" ]
    ( action-code = "DPR" or action-code = "IPR" or action-code = "day-perception-range" or action-code = "visual-range" ) [ "visual-range" ]
    ( action-code = "DPA" or action-code = "day-perception-angle" or action-code = "visual-angle" ) [ "visual-angle" ]
    ( action-code = "NPR" or action-code = "night-perception-range" or action-code = "day-perception" ) [ "day-perception" ]
    ( action-code = "NPA" or action-code = "night-perception-angle" or action-code = "night-perception" ) [ "night-perception" ]
    ( action-code = "APR" or action-code = "audio-perception" or action-code = "audio-perception-range" ) [ "audio-perception-range" ] ; not used
    ( action-code = "APA" or action-code = "audio-perception-angle" ) [ "audio-perception-angle" ] ; not used
    ( action-code = "VCR" or action-code = "vocal-range" ) [ "vocal-range" ] ; not used
    ( action-code = "ICC" or action-code = "conception-chance" ) [ "conception-chance" ]
    ( action-code = "STM" or action-code = "stomach-size" ) [ "bite-capacity" ]   
    ( action-code = "MTR" or action-code = "mutation-rate" or  action-code = "MTC" or action-code = "mutation-chance") [ "mutation-chance" ]
    ( action-code = "SXR" or action-code = "sex-ratio" ) [ "sex-ratio" ]
    ( action-code = "LTS" or action-code = "litter-size" ) [ "litter-size" ]
    ( action-code = "TRT" or action-code = "turn-right" ) [ "turn-right" ]
    ( action-code = "TLF" or action-code = "turn-left" ) [ "turn-left" ]
    ( action-code = "GFD" or action-code = "go-forward" ) [ "go-forward" ]
    ( action-code = "MOV" or action-code = "move-toward" ) [ "move-toward" ]
    ( action-code = "move-away-from" ) [ "move-away-from" ]
    ( action-code = "HED" or action-code = "set-heading" ) [ "set-heading" ]
    ( action-code = "set-heading-random" ) [ "set-heading-random" ]
    ( action-code = "HID" or action-code = "hide" ) [ "hide" ]
    ( action-code = "rest" ) [ "rest" ]
    ( action-code = "SAC" or action-code = "signal-a-chance" or action-code = "SAO" or action-code = "signal-a-on" or action-code = "signal-alpha-on" or action-code = "yellow-signal-on" or action-code = "yellow-signal" ) [ "yellow-signal" ]
    ( action-code = "SBC" or action-code = "signal-b-chance" or action-code = "SBO" or action-code = "signal-b-on" or action-code = "signal-beta-on" or action-code = "red-signal-on" or action-code = "red-signal" ) [ "red-signal" ]
    ( action-code = "SCC" or action-code = "signal-c-chance" or action-code = "SCO" or action-code = "signal-c-on" or action-code = "signal-gamma-on" or action-code = "blue-signal-on" or action-code = "blue-signal") [ "blue-signal" ]
    ( action-code = "INC" or action-code = "infancy-chance" or action-code = "CKI" or action-code = "check-infancy" ) [ "check-infancy" ]
    ( action-code = "GVB" or action-code = "birthing-chance" or action-code = "CKB" or action-code = "check-birth" ) [ "check-birth" ]
    ( action-code = "JVC" or action-code = "juvenile-chance" or action-code = "CKJ" or action-code = "check-juvenility" ) [ "check-juvenility" ]
    ( action-code = "WEN" or action-code = "weaning-chance" or action-code = "CKW" or action-code = "check-weaning" ) [ "check-weaning" ]
    ( action-code = "ADC" or action-code = "adult-chance" or action-code = "CKA" or action-code = "check-adulthood" ) [ "check-adulthood" ]
    ( action-code = "SNC" or action-code = "senescent-chance" ) [ "senescent-chance" ] ; not used
    ( action-code = "CKS" or action-code = "check-senescence" ) [ "check-senescence" ] ; not used
    ( action-code = "DSC" or action-code = "digest-stomach" ) [ "digest-stomach" ] ; not used
    ( action-code = "FAT" or action-code = "reserve-energy" ) [ "reserve-energy" ] ; not used
    ( action-code = "BRN" or action-code = "burn-reserves" ) [ "burn-reserves" ] ; not used
    ( action-code = "GFT" or action-code = "supply-to" ) [ "supply-to" ]
    ( action-code = "RFT" or action-code = "demand-from" ) [ "demand-from" ]
    ( action-code = "EAT" or action-code = "eat") [ "eat" ]
    ( action-code = "PKU" or action-code = "pick-up" ) [ "pick-up" ]
    ( action-code = "SQM" or action-code = "squirm-from" ) [ "squirm-from" ]
    ( action-code = "PDN" or action-code = "put-down" ) [ "put-down" ]
    ( action-code = "CLG" or action-code = "cling-to" ) [ "cling-to" ]
    ( action-code = "ATK" or action-code = "attack" ) [ "attack" ]
    ( action-code = "HLP" or action-code = "help") [ "help" ]
    ( action-code = "JNG" or action-code = "join-group-of" or action-code = "join" ) [ "join" ]
    ( action-code = "LEV" or action-code = "leave-group" or action-code = "LEV" or action-code = "leave" ) [ "leave" ]
    ( action-code = "RCT" or action-code = "recruit" ) [ "recruit" ]
    ( action-code = "KCK" or action-code = "kick-out" or action-code = "expel" ) [ "expel" ]
    ( action-code = "MAT" or action-code = "mate-with") [ "mate-with" ]
    [ action-code ])
  
end

to-report status-of [ target ]
  let status-report ""

  set status-report ( word status-report ifelse-value ( get-solar-status = "DAY" ) [ "*" ][ "-" ] )
  
  if (is-anima1? target) [ ; ANIMALS
    let target-kind "z"
    let target-self ifelse-value ( target = self ) [ "e" ] [ "o" ]
    let target-alive ifelse-value ( is.alive ) [ "v" ] [ "w" ]
    let target-sex first [biological.sex] of target
    let target-life-history first [life.history] of target
    let target-fertility first [female.fertility] of target
    let target-size ifelse-value ( [size] of target < size ) [ "r" ] [ "b" ]
    let target-health ifelse-value ( [living.chance] of target > living.chance ) [ "h" ] [ "x" ]
    let target-kin ifelse-value ( relatedness-with target > 0.2 ) [ "k" ] [ "q" ]
    let target-group ifelse-value ( [group.identity] of target = group.identity ) [ "n" ] [ "u" ] 
    let target-carry ifelse-value ( member? target carried.items ) [ "y" ] [ ifelse-value ( member? self [carried.items] of target ) [ "+" ] [ "t" ] ]
    let target-signal (word ifelse-value [yellow.signal] of target [ "α" ][ "å" ] ifelse-value [red.signal] of target [ "β" ][ "ß" ] ifelse-value [blue.signal] of target [ "ɣ" ][ "œ" ] )
    let target-rest ifelse-value ( is.resting = true ) [ "s" ] [ "." ]
    let target-color (word "" round [color] of target "")
    set status-report (word status-report 
      target-kind 
      target-self 
      target-alive 
      target-sex 
      target-life-history
      ifelse-value ( target = self or model-structure = "show-fertility" ) [ target-fertility ] [ "" ]
      target-size 
      target-health 
      target-kin 
      target-group 
      target-carry 
      target-signal
      target-rest 
      target-color ) ]
    
  if ( is-patch? target ) [ ; PATCHES
    let target-kind "d" 
    let target-pcolor (word "" round [pcolor] of target "")
    set status-report (word status-report word target-kind target-pcolor )]
  
  report status-report
end

to-report first-string-is-part-of-last? [ string1 string2 ]
  let first-is-part-of-last true
  let index 0
  if ( is-string? string1 and is-string? string2 ) [
    let string1-length length string1 ; saves on runtime
    while [ first-is-part-of-last and index < string1-length ] [
      if ( not member? item index string1 string2 ) and ( item index string1 != " " ) [ set first-is-part-of-last false ]
      set index index + 1 ]]
  report first-is-part-of-last
end

to-report status-code-list
  report [ 
    "a" "b" "c" "d" "e" "f" "g" 
    "h" "i" "j" "k" "l" "m" "n"
    "o" "p" "q" "r" "s" "t" "u"
    "v" "w" "x" "y" "z" "+" "α" 
    "å" "β" "ß" "ɣ" "œ" "*" "-" 
    "#" "$" "~" "1" "2" "3" "4" 
    "5" "6" "7" "8" "9" "0" "." ]
end

to-report action-code-list
  report [ 
    "living-chance" "body-size" "body-shade" 
    "day-perception" "night-perception" "visual-angle" "visual-range" 
    "conception-chance" "bite-capacity" "mutation-chance" "sex-ratio" "litter-size" 
    "turn-right" "turn-left" "go-forward" "set-heading" "hide" "rest" "set-heading-random"
    "yellow-signal" "red-signal" "blue-signal" 
    "check-infancy" "check-birth" "check-juvenility" "check-weaning" "check-adulthood" 
    "move-toward" "move-away-from" "supply-to" "demand-from" "eat"
    "join" "leave" "recruit" "kick-out"
    "pick-up" "put-down" "cling-to" "squirm-from"
    "help" "hurt" "mate-with" ]
end

to-report generate-status
  let new-status one-of status-code-list
  repeat ( random 5 ) [ set new-status ( word new-status one-of status-code-list ) ]
  report new-status
end
