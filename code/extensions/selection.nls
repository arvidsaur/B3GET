 ;-------------------------------------------------------------------------------------------------                                                                                                                                                   
;
;                    dP                     dP   oo                   
;                    88                     88                        
;  .d8888b. .d8888b. 88 .d8888b. .d8888b. d8888P dP .d8888b. 88d888b. 
;  Y8ooooo. 88ooood8 88 88ooood8 88'  `""   88   88 88'  `88 88'  `88 
;        88 88.  ... 88 88.  ... 88.  ...   88   88 88.  .88 88    88 
;  `88888P' `88888P' dP `88888P' `88888P'   dP   dP `88888P' dP    dP 
;
; Â© 2020 K N Crouse     
;-------------------------------------------------------------------------------------------------
; 
; This extension contains all functions that allow users to apply artificial selection
; on organismal populations. Selection can be turned ON or OFF with the SELECTION-ON? 
; switch on the user interface. When ON is selected, the ARTIFICIAL-SELECTION function is 
; applied once every timestep and artificially selects for a population that can better
; persist in its environment.
;
;-------------------------------------------------------------------------------------------------

;-------------------------------------------------------------------------------------------------
; ARTIFICIAL-SELECTION
;
; Operation that allows an override of B3GET to artificially select for populations that are
; better able to survive in the environmental conditions set by the model parameters.
; 
; The genotype files incrememntally show the selection process. The population files record each population
; before the reset occurs, whether successful or not
;-------------------------------------------------------------------------------------------------

to artificial-selection ;  memory caps?
  
  if ( any? anima1s ) [
    
    ; MUTATE GENOTYPES
    if ( ticks = 0 ) [ ask anima1s [ mutate-chromosomes 0.05 ]] ; increases the genetic variation of the initial population
    
    ; SET GENOTYPE TO PROPER STANDARDIZED NAME
    if ( genotype = "" ) [
      set genotype generate-genotype-id
    ]
    
    let number-list [ "1" "2" "3" "4" "5" "6" "7" "8" "9" "0" ]
    if ( not member? "-" genotype or not member? last genotype number-list ) [
      set genotype ( word genotype "-0" )
      ask one-of anima1s [ save-genotype ]]
    
    impose-selection
    
    ; RESET SIMULATION
    if ( restart-simulation? ) [ ; if criteria met for restarting simulation
      
      simulation-summary
      
      let selected-anima1 max-one-of anima1s with [ life.history = "adult" ] [ generation.number ]
      if ( [generation.number] of selected-anima1 > genotype-number ) [
        set genotype (word (genotype-name) "-" ( genotype-number + 1 ) ) ; set new genotype name up one more number increment
        ask selected-anima1 [ save-genotype ]]
      
      let old-population-name population
      set population next-population-file-name
      save-population
      set population old-population-name
      
      setup
    ]
  ]
end

to-report dash-position 
  report ( position "-" genotype ) 
end

to-report genotype-number
  report read-from-string substring genotype ( dash-position + 1 ) ( length genotype )
end

to-report genotype-name
  report substring genotype 0 ( dash-position )
end

to impose-selection
  
  if ( count anima1s with [ is.alive ] > 100 and random-float 1.0 < 0.01 ) [
    
    let min-generation min [generation.number] of anima1s with [ is.alive ]
    if ( any? anima1s with [ is.alive and generation.number > min-generation ] ) [ ; if any agents who have more than min generation (aka there is variation for this trait)
      ask one-of anima1s with [ is.alive and generation.number = min-generation ] [ ; select one of earliest generation agents to die
        set-to-dead ; agent dies but body remains and decomposes
        set cause.of.death "selected to die" ]
    ]
  ]
  
end

to-report restart-simulation?
  let criteria-met false
  
  if ( not any? anima1s with [ biological.sex = "female" ] or not any? anima1s with [ biological.sex = "male" ] ) [ set criteria-met true ]
  
  let ticks-scale 10000 ; how many ticks (max) transpire between mean generations
  if ( ticks > ticks-scale * ( mean [generation.number] of anima1s with [ is.alive ] + 0.5 )) [
    set criteria-met true
  ]

  report criteria-met
end

to-report next-population-file-name
  let the-alphabet [ "A" "B" "C" "D" "E" "F" "G" "H" "I" "J" "K" "L" "M" "N" "O" "P" "Q" "R" "S" "T" "U" "V" "W" "X" "Y" "Z" ]
  let alphabet-length length the-alphabet
  let base-file-name ( word population "-" last genotype "-" )
  let new-letter-code "A"
  let pre-letter-code ""
  let current-letter "A"
  let post-letter-code ""
  while [ file-exists? ( word path-to-experiment base-file-name new-letter-code ".csv" ) ]
  [ 
    let position-of-current-letter position current-letter the-alphabet
    
    ifelse ( position-of-current-letter + 1 >= alphabet-length )[
      
      ifelse ( length pre-letter-code = 0 ) [
        
        let this-letter-code ( word "AA" post-letter-code )
        set pre-letter-code but-last this-letter-code
        set current-letter last this-letter-code
        set post-letter-code ""
        
      ][
        set current-letter last pre-letter-code
        set pre-letter-code but-last pre-letter-code
        set post-letter-code ( word "A" post-letter-code )
      ]
    ][
      set current-letter ( item ( position-of-current-letter + 1 ) the-alphabet )
      let this-letter-code ( word pre-letter-code current-letter post-letter-code )
      set pre-letter-code but-last this-letter-code
      set current-letter last this-letter-code
      set post-letter-code ""
    ]
    
    set new-letter-code ( word pre-letter-code current-letter post-letter-code )
  ]
  report ( word base-file-name new-letter-code )
end

